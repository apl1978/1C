// обработка парсит все заранее сохраненые HTML-файлы из указанного каталога. В данном случае структура настроена на шаблон
// анкетирования WordPress. 
// разбирается дерево DOM, ответы хранятся в JSON, который тоже разбирается. Картинки к ответам загружаются по прямым ссылкам, полученным
// при разборе HTML. Полученные вопросы с картинками и правильным ответом пишутся в справочники самописной конфигурации.

&НаСервере
Процедура ЗагрузкаНаСервере()   
	
	Если ОбъектТестирования.Пустая() Тогда
		Сообщить("Выберите или создайте новый объект тестирования и повторите загрузку");
		Возврат;
	КонецЕсли;
	
	
	МассивФайлов = НайтиФайлы("C:\tests\", "*.html", ложь);
	Для каждого ЭлементМассива Из МассивФайлов Цикл
		ЧтениеHTML = Новый ЧтениеHTML;         
		ЧтениеHTML.ОткрытьФайл(ЭлементМассива.ПолноеИмя, "UTF8");
              
		ПостроительDOM = Новый ПостроительDOM;        
		ДокументDOM = ПостроительDOM.Прочитать(ЧтениеHTML); 
		
		// читаем текущий раздел
		//Сообщить(СокрЛП(ДокументDOM.ПолучитьЭлементыПоИмени("title")[0].ТекстовоеСодержимое));
		Раздел = (СокрЛП(ДокументDOM.ПолучитьЭлементыПоИмени("h1")[0].ТекстовоеСодержимое));
		Раздел = Лев(Раздел, СтрДлина(Раздел)-3);
		СсылкаРаздел = Справочники.Разделы.НайтиПоНаименованию(Раздел);
		Если СсылкаРаздел.Пустая() Тогда
			ОбъектРаздел = Справочники.Разделы.СоздатьЭлемент();
			ОбъектРаздел.Владелец = ОбъектТестирования;
			ОбъектРаздел.Наименование = Раздел;
			ОбъектРаздел.Записать();
			СсылкаРаздел = ОбъектРаздел.Ссылка;
		КонецЕсли;	
		
		
		// собираем вопросы и ответы к ним
		ЭлементыHTML = ДокументDOM.ПолучитьЭлементыПоИмени("div");                
		МассивВопросов = Новый Массив;
		Для каждого ЭлДив Из ЭлементыHTML Цикл
			
			Если ЭлДив.ИмяКласса = "wpProQuiz_question" Тогда
			    // нашли блок вопроса
				Для Каждого Эл ИЗ ЭлДив.ДочерниеУзлы Цикл
					Если Эл.ИмяУзла = "div" Тогда	
						// нашли блок текста этого вопроса
						//Сообщить(СокрЛП(Эл.ТекстовоеСодержимое));
						Вопрос = СокрЛП(Эл.ТекстовоеСодержимое);
						СсылкаВопрос = Справочники.Вопросы.НайтиПоРеквизиту("Вопрос", Вопрос,,ОбъектТестирования);
						Если СсылкаВопрос.Пустая() Тогда
							ОбъектВопрос = Справочники.Вопросы.СоздатьЭлемент();
							ОбъектВопрос.Владелец = ОбъектТестирования;
							ОбъектВопрос.Раздел = СсылкаРаздел;
							ОбъектВопрос.Вопрос = Вопрос;
							ОбъектВопрос.Код = Лев(Вопрос, 5);
							ОбъектВопрос.НомерВопросаВКомплекте = Сред(Вопрос,4,2);
						Иначе
							ОбъектВопрос = 	СсылкаВопрос.ПолучитьОбъект();
						КонецЕсли;	
						
						//сохраняем ссылку на картинку
						Если Эл.ПолучитьЭлементыПоИмени("a").Количество() > 0 Тогда
							//в вопросе есть картинка, сохраняем

						    Соединение = Новый HTTPСоединение("1-exam.ru");
						    Запрос = Новый HTTPЗапрос(Эл.ПолучитьЭлементыПоИмени("a")[0].Гиперссылка);
						    Ответ = Соединение.Получить(Запрос);
							ДвоичныеДанныеКартинки = Ответ.ПолучитьТелоКакДвоичныеДанные();							
							
							ОбъектВопрос.Картинка = Новый ХранилищеЗначения(ДвоичныеДанныеКартинки, Новый СжатиеДанных());
							
						КонецЕсли;	
						
						НомерОтвета = 1;
					ИначеЕсли Эл.ИмяУзла = "ul" Тогда		
						// нашли блок ответов этого вопроса
						Для Каждого ЭлОтвет Из Эл.ДочерниеУзлы Цикл
							Если ЭлОтвет.ИмяУзла = "li" Тогда
								//Сообщить(Строка(НомерОтвета) + " " + СокрЛП(ЭлОтвет.ТекстовоеСодержимое));
								Ответ = Строка(НомерОтвета) + " " + СокрЛП(ЭлОтвет.ТекстовоеСодержимое);
								ОбъектВопрос["Ответ"+НомерОтвета] = Ответ;
								НомерОтвета = НомерОтвета + 1;
							КонецЕсли;	
						КонецЦикла;
						ОбъектВопрос.Записать();
						МассивВопросов.Добавить(ОбъектВопрос);
					КонецЕсли;	
				КонецЦикла;
			КонецЕсли;	
		КонецЦикла;
		
		// парсим блок ответов
		ЭлементыHTML = ДокументDOM.ПолучитьЭлементыПоИмени("script");
		Для Каждого ЭлСкрипт Из ЭлементыHTML Цикл
			Если Найти(ЭлСкрипт.ТекстовоеСодержимое, "json:") > 0 Тогда
				СтрокаJSON = СокрЛП(ЭлСкрипт.ТекстовоеСодержимое);
				СтрокаJSON = Прав(СтрокаJSON, СтрДлина(СтрокаJSON) - Найти(СтрокаJSON, "json")-6);
				СтрокаJSON = Лев(СтрокаJSON,СтрДлина(СтрокаJSON)-14);
			КонецЕсли;	
		КонецЦикла;	
		ЧтениеHTML.Закрыть();            
		
		Если СтрДлина(СтрокаJSON) > 0 Тогда 
			// читаем JSON с ответами
			ЧтениеJSON = Новый ЧтениеJSON();
			ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
			ИндексВопроса = 0;
			Пока ЧтениеJSON.Прочитать() Цикл
				
				Структура = Прочитатьjson(ЧтениеJSON);
				Если ТипЗнч(Структура) = Тип("Структура") Тогда
					// ответы лежат в массиве correct, индекс в массиве равен номеру ответа
					// правильный ответ имеет значение = 1
					//Сообщить(Структура.Correct.Найти(1)+1);
					ПравильныйОтвет = Структура.Correct.Найти(1)+1;
					ОбъектВопрос = МассивВопросов[ИндексВопроса];
					ОбъектВопрос.ПравильныйВариантОтвета = ПравильныйОтвет;
					ОбъектВопрос.Записать();
					ИндексВопроса = ИндексВопроса + 1;
				Иначе
				КонецЕсли;	
				
			КонецЦикла;	
			ЧтениеJSON.Закрыть();
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	ЗагрузкаНаСервере();
	Предупреждение("Загрузка завершена");
КонецПроцедуры
