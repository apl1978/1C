// Обработка подсчитывает количество элементов во всех справочниках, документах, регистрах, бизнес-процессах и задачах.
// Бывает полезна при оценке переноса данных, свертке и т.д.
// Код работает и на управляемых формах и на обычных. Для обычных форм надо в строке Объект[Мета].Загрузить(Запрос.Выполнить().Выгрузить());
// заменить Объект на ЭтотОбъект и убрать директивы компиляции &НаКлиенте, &НаСервере.
// Обработка содержит 7 табличных частей названных так же как ветки в Метаданных: "Справочники", "Документы" и т.д.
// Каждая ТЧ содержит два реквизита: ВсегоЭлементов тип Число (10, 0) и ИмяМета тип Строка (200).

&НаКлиенте
Процедура Сформировать(Команда)
	
	РазмерМета("Справочники", "Справочник");
	РазмерМета("Документы", "Документ");
	РазмерМета("РегистрыСведений", "РегистрСведений");
	РазмерМета("РегистрыНакопления", "РегистрНакопления");
	РазмерМета("РегистрыБухгалтерии", "РегистрБухгалтерии");
	РазмерМета("БизнесПроцессы", "БизнесПроцесс");
	РазмерМета("Задачи", "Задача");
	
КонецПроцедуры

&НаСервере
Процедура РазмерМета(Мета, МетаДляЗапроса)

	Всего = 0;

	Запрос = Новый Запрос;
	Запрос.Текст = "";
	
	ШаблонТекста= "ВЫБРАТЬ
	|    КОЛИЧЕСТВО(*) КАК ВсегоЭлементов,
	|	 ""ИмяТаблицы"" КАК ИмяМета
	|ИЗ
	|    %Мета%.ИмяТаблицы";
	
	ШаблонТекста = СтрЗаменить(ШаблонТекста, "%Мета%", МетаДляЗапроса);
	
	Для Каждого Спр Из Метаданные[Мета] Цикл
        
		ЗапросМета = СтрЗаменить(ШаблонТекста, "ИмяТаблицы", Спр.Имя);
		
		Если Запрос.Текст = "" Тогда
			Запрос.Текст = ЗапросМета
		Иначе
			Запрос.Текст = Запрос.Текст + " ОБЪЕДИНИТЬ ВСЕ " + ЗапросМета;
		КонецЕсли;	
		
	КонецЦикла;
	
	Если Не Запрос.Текст = "" Тогда
		Объект[Мета].Загрузить(Запрос.Выполнить().Выгрузить());
    // для обычных форм
    // ЭтотОбъект[Мета].Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли;	
	
КонецПроцедуры
