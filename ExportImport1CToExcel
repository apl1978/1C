//обработка запросом собирает данные, выгружает в табличную часть. после этого можно выгрузить данные в файл xls, обработать в Excel,
//загрузить обратно и новые данные записать в периодический регистр сведений (нужно для истории изменений)
// реквизиты обработки: ДатаЗагрузки(Дата), СрокДействия(Дата). Табличная часть ФизЛица (Контрагент, Договор, НеплательщикНДС, НомерДоговора,
// ДатаДоговора, КодКонтрагента, ИНН)

//Обработка работает так:
//1. Нажимаем "Заполнить данными 1С", табличная часть заполняется данными по условию задачи.
//2. Нажимаем "Выгрузить в файл". Откроется диалог выбора, куда сохранить файл и туда запишется xls-файл со всем содержимым табличной
//части, полученном на шаге 1.
//3. Пользователь открывает полученный xls-файл и проставляет любой символ в колонке Неплательщик НДС. Не важно, что ставить "1", "да",
//"v" и т.д. Обработка настроена так, что сработает на любое заполненное значение. Главное, чтобы хоть что-то было.
//А тем, кто НЕ неплательщик НДС надо оставить пустое значение. Нельзя туда писать "нет" и т.п. Просто оставить пусто. Нельзя менять
//структуру файла, удалять шапку и столбцы. Менять местами столбцы тоже нельзя. Обработка не проверяет формат файла и считает,
//что он не изменился с момента выгрузки. Сохранить файл и закрыть его в Excel. 
//4. В обработке нажать кнопку "Загрузить из файла". Откроется диалог выбора файла xls. После выбора файла, в табличной части появятся
//данные. По тем строкам, где в файле в колонке "НеплательщикНДС" были введены значения проставятся галочки. При необходимости,
//их можно в обработке откорректировать вручную.
//5. Указываем дату загрузки.
//6. Нажимаем "Записать в регистр". 1С проверит, указана ли дата загрузки и если нет - не будет записывать и сообщит об этом.
//Если дата указана и табличная часть не пуста И в табличной части есть строки с галочками "Неплательщик НДС", то такие строки
//загрузятся в регистр. Другими  словами - в регистр запишутся только строки, у которых есть галочка НеплательщикНДС датой
//"Дата загрузки".
//7. При желании, пункты со 2 по 4 можно пропустить. Можно просто нажать "Заполнить данными 1С", проставить в нужных строках галочки,
//указать "Дату загрузки" и нажать "Записать в регистр"



Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ДиалогФыбораФайла								=	Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогФыбораФайла.Фильтр						=	"Файл данных (*.xls)|*.xls|Файл данных (*.xlsx)|*.xlsx";
	ДиалогФыбораФайла.Расширение					=	"xls";	
	ДиалогФыбораФайла.Заголовок						=	"Выберите файл данных";
	ДиалогФыбораФайла.ПредварительныйПросмотр		=	Ложь;
	ДиалогФыбораФайла.ИндексФильтра					=	0;
	ДиалогФыбораФайла.ПолноеИмяФайла				=	"";
	ДиалогФыбораФайла.ПроверятьСуществованиеФайла	=	Ложь;

	Если ДиалогФыбораФайла.Выбрать() Тогда
		ИмяФайла = ДиалогФыбораФайла.ПолноеИмяФайла;
	Иначе
		Возврат;
	КонецЕсли;
		
	Попытка
		Excel = Новый COMОбъект("Excel.Application");
		Состояние("Выгрузка данных из 1С в Microsoft Excel...");
		Книга = Excel.WorkBooks.Add();
		ТекЛист = Книга.WorkSheets(1);
		
		ТекЛист.Cells(1, 1).Value = "Контрагент";
		ТекЛист.Cells(1, 2).Value = "Код контрагента";
		ТекЛист.Cells(1, 3).Value = "ИНН";
		ТекЛист.Cells(1, 4).Value = "Договор";
		ТекЛист.Cells(1, 5).Value = "Номер договора";
		ТекЛист.Cells(1, 6).Value = "Дата договора";
		ТекЛист.Cells(1, 7).Value = "Код договора";
		ТекЛист.Cells(1, 8).Value = "Неплательщик НДС";
		
		Для Колонка = 1 По 8 Цикл
			ТекЛист.Cells(1, Колонка).Borders.LineStyle = 1;
			ТекЛист.Cells(1, Колонка).Font.Bold = 1;
			ТекЛист.Cells(1, Колонка).HorizontalAlignment = 3;
		КонецЦикла;	
		
		СтрокаЛиста = 2;
		Для Каждого СтрФизЛиц Из ФизЛица Цикл
			
			Для Колонка = 1 По 8 Цикл
				ТекЛист.Cells(СтрокаЛиста, Колонка).Borders.LineStyle = 1;
			КонецЦикла;
			
			ТекЛист.Cells(СтрокаЛиста, 1).Value = СокрЛП(СтрФизЛиц.Контрагент.Наименование);
			ТекЛист.Cells(СтрокаЛиста, 2).NumberFormat = "@";
			ТекЛист.Cells(СтрокаЛиста, 2).Value = СокрЛП(СтрФизЛиц.КодКонтрагента);
			ТекЛист.Cells(СтрокаЛиста, 3).NumberFormat = "@";
			ТекЛист.Cells(СтрокаЛиста, 3).Value = СокрЛП(СтрФизЛиц.ИНН);
			ТекЛист.Cells(СтрокаЛиста, 4).Value = СокрЛП(СтрФизЛиц.Договор.Наименование);
			ТекЛист.Cells(СтрокаЛиста, 5).NumberFormat = "@";
			ТекЛист.Cells(СтрокаЛиста, 5).Value = СокрЛП(СтрФизЛиц.НомерДоговора);
			ТекЛист.Cells(СтрокаЛиста, 6).Value = Формат(СтрФизЛиц.ДатаДоговора, "ДФ=dd.MM.yyyy");
			ТекЛист.Cells(СтрокаЛиста, 7).NumberFormat = "@";
			ТекЛист.Cells(СтрокаЛиста, 7).Value = СокрЛП(СтрФизЛиц.Договор.Код);
			
			СтрокаЛиста = СтрокаЛиста + 1;
			
		КонецЦикла;	
		
		ТекЛист.Columns.AutoFit();
		
		Книга.SaveAs(ИмяФайла);
		
		Сообщить("Файл выгружен успешно: " + ИмяФайла);
		
		Excel.Quit();
		
	Исключение
		Сообщить("Ошибка при запуске Microsoft Excel."
        + Символы.ПС + ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

Процедура ОсновныеДействияФормыЗагрузить(Кнопка)
	
	ДиалогФыбораФайла								=	Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогФыбораФайла.Фильтр						=	"Файл данных (*.xls)|*.xls|Файл данных (*.xlsx)|*.xlsx";
	ДиалогФыбораФайла.Расширение					=	"xls";	
	ДиалогФыбораФайла.Заголовок						=	"Выберите файл данных";
	ДиалогФыбораФайла.ПредварительныйПросмотр		=	Ложь;
	ДиалогФыбораФайла.ИндексФильтра					=	0;
	ДиалогФыбораФайла.ПолноеИмяФайла				=	"";
	ДиалогФыбораФайла.ПроверятьСуществованиеФайла	=	Ложь;

	Если ДиалогФыбораФайла.Выбрать() Тогда
		ИмяФайла = ДиалогФыбораФайла.ПолноеИмяФайла;
	Иначе
		Возврат;
	КонецЕсли;
	
	ФизЛица.Очистить();
	
	Попытка
		Excel = Новый COMОбъект("Excel.Application");
		Excel.WorkBooks.Open(ИмяФайла);
		Состояние("Обработка файла Microsoft Excel...");
		ТекЛист = Excel.Sheets(1);
	Исключение
		Сообщить("Ошибка. Возможно неверно указан номер листа книги Excel.");
		Возврат;
	КонецПопытки;
    ВсегоСтрок = ТекЛист.Cells(1,1).CurrentRegion.Rows.Count();
	ВсегоКолонок = ТекЛист.Cells(1,1).CurrentRegion.Columns.Count();
	
	Для СчСтрок = 2 По ВсегоСтрок Цикл
		Договор = СокрЛП(ТекЛист.Cells(СчСтрок,7).Value);
		НеплательщикНДС = СокрЛП(ТекЛист.Cells(СчСтрок,8).Value);
		
		СтрокаФизЛиц = ФизЛица.Добавить();
		СтрокаФизЛиц.Договор = Справочники.ДоговорыКонтрагентов.НайтиПоКоду(Договор);
		СтрокаФизЛиц.НеплательщикНДС = ?(НеплательщикНДС = "", Ложь, Истина);
		СтрокаФизЛиц.Контрагент = СтрокаФизЛиц.Договор.Владелец;
		СтрокаФизЛиц.НомерДоговора = СтрокаФизЛиц.Договор.Номер;
		СтрокаФизЛиц.ДатаДоговора = СтрокаФизЛиц.Договор.Дата;
		СтрокаФизЛиц.КодКонтрагента = СтрокаФизЛиц.Контрагент.Код;
		СтрокаФизЛиц.ИНН = СтрокаФизЛиц.Контрагент.ИНН;
		
	КонецЦикла;	
	
	Excel.Quit();
	
КонецПроцедуры

Процедура КоманднаяПанель1Заполнить(Кнопка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
		|	ДоговорыКонтрагентов.Номер КАК НомерДоговора,
		|	ДоговорыКонтрагентов.Дата КАК ДатаДоговора,
		|	ДоговорыКонтрагентов.Владелец.Код КАК КодКонтрагента,
		|	ДоговорыКонтрагентов.Владелец.ИНН КАК ИНН
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
		|	И ДоговорыКонтрагентов.Владелец.ЮрФизЛицо = &ЮрФизЛицо
		|	И НЕ ДоговорыКонтрагентов.ПометкаУдаления";
		
	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	Запрос.УстановитьПараметр("ЮрФизЛицо", Перечисления.ЮрФизЛицо.ФизЛицо);
	
	Если Не СрокДействия = Дата('00010101') Тогда
		Запрос.УстановитьПараметр("СрокДействия", СрокДействия);
		Запрос.Текст = Запрос.Текст + " И ДоговорыКонтрагентов.СрокДействия >= &СрокДействия ";
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ФизЛица.Загрузить(РезультатЗапроса.Выгрузить());

КонецПроцедуры

Процедура ОсновныеДействияФормыДействие(Кнопка)
	
	Если ДатаЗагрузки = Дата('00010101') Тогда
		Предупреждение("Укажите дату загрузки");
		Возврат;
	КонецЕсли;
	
	Если ФизЛица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	Для Каждого СтрФизЛиц Из ФизЛица Цикл
		Если СтрФизЛиц.НеплательщикНДС Тогда
			НоваяЗапись = РегистрыСведений.НеплательщикиНДС.СоздатьМенеджерЗаписи();
			НоваяЗапись.Период = ДатаЗагрузки;
			НоваяЗапись.Договор = СтрФизЛиц.Договор;
			НоваяЗапись.Прочитать();
			
			Если Не НоваяЗапись.Выбран() Тогда
				НоваяЗапись.Период = ДатаЗагрузки;
				НоваяЗапись.Договор = СтрФизЛиц.Договор;
			КонецЕсли;	
			
			НоваяЗапись.НеплательщикНДС = СтрФизЛиц.НеплательщикНДС;
			НоваяЗапись.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

