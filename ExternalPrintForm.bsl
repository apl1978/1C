// Внешняя печатная форма (ВПФ) для конфигурации на управляемых формах. Метод подойдет для любой конфигурации, 
// построенной на БСП не ниже 3.0. В данном случае конфигурация ЗУП 3.1
// В таких конфигурациях ВПФ может быть только обработкой. Но данные удобнее собирать с помощью СКД.
// Реквизит "Основная схема компоновки данных" есть только в объекте "Отчет", который нельзя подключтить как ВПФ. Как не меняя конфигурацию
// собрать данные внешним отчетом и вывести результат во внешнюю печатную форму - решение ниже.
// Из конфигурации был выгружен типовой отчет "ОтчетыПоСотрудникам", были внесены изменения в запрос СКД и в процедуру вывода результатов
// в макет, создан новый макет печатной формы. В модуль отчета добавлены процедуры и функции для регистрации его в базе как внешнего.
// А так же реквизиты, позволяющие разным ВПФ вызывать один и тот же отчет для разного заполнения и вывода в разные макеты.
// Функция СведенияОВнешнейОбработке() внешнего отчета:
Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = Новый Структура;
	МассивНазначений = Новый Массив;
	
	ПараметрыРегистрации.Вставить("Вид", "Отчет");
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
	ПараметрыРегистрации.Вставить("Наименование", "Отчеты по сотрудникам служебный");
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Истина);
	ПараметрыРегистрации.Вставить("Информация", "Отчеты по сотрудникам служебный. Не использовать");
	
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	
	ДобавитьКоманду(ТаблицаКоманд,
	"Отчеты по сотрудникам служебный",
	"ПФ_MXL_Т2",
	"ВызовСерверногоМетода",
	Истина,
	"ПечатьMXL");
	
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

//************конец отчета*****************

// отчет был подключен как внешний. Автоматически получил заданное наименование ("Отчеты по сотрудникам служебный") в справочнике
// "ДополнительныеОтчетыИОбработки". Дальше в была написана обработка, вызывающая этот внешний отчет из справочника,
// передающая и в него параметры вызова. И получающая обратно сформированную печатную форму "ДокументРезультат".
// Далее типовым методом форма выводится на экран пользователя.
// Все это происходит в функции ПечатьТ2().

//***********начало обработки**************

// полный модуль объекта внешней обработки - печатной формы
Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = Новый Структура;
	МассивНазначений = Новый Массив;
	МассивНазначений.Добавить("Справочник.Сотрудники");
	
	ПараметрыРегистрации.Вставить("Вид", "ПечатнаяФорма");
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
	ПараметрыРегистрации.Вставить("Наименование", "Личная карточка (Т-2)");
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
	ПараметрыРегистрации.Вставить("Информация", "Дополнительная внешняя печатная форма Личная карточка (Т-2)");
	
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	
	ДобавитьКоманду(ТаблицаКоманд,
	"Личная карточка (Т-2)",
	"ПФ_MXL_Т2",
	"ВызовСерверногоМетода",
	Истина,
	"ПечатьMXL");
	
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()
	
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	Возврат Команды;
	
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ИменаМакетов, ПараметрыВывода) Экспорт
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
	КоллекцияПечатныхФорм,
	"ПФ_MXL_Т2", НСтр("ru='Личная карточка (Т-2)'"),
	ПечатьТ2(МассивОбъектов), ,);
	
КонецПроцедуры // Печать()

// Процедура печати Т-2.
// Возвращает табличный документ - сформированную унифицированную форму Т-2.
//
// Параметры:
//	МассивОбъектов - массив сотрудников.
//  ОбъектыПечати  - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя области в
//                   которой был выведен объект).
//
// Возвращаемое значение:
//	Табличный документ
//
Функция ПечатьТ2(МассивОбъектов) Экспорт
	
	МойОтчет = Справочники.ДополнительныеОтчетыИОбработки.НайтиПоНаименованию("Отчеты по сотрудникам служебный");
	ОтчетТ2 = ДополнительныеОтчетыИОбработкиВызовСервера.ОбъектВнешнейОбработки(МойОтчет);
	
  // если в отчете взвести реквизит ВУР, сформируется отчет Т2 по утвержденной форме ВУР. Иначе - обычная форма Т2, измененная по
  // требованию заказчика.
  //ОтчетТ2.ВУР = ИСТИНА;
  

	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.АвтоМасштаб = Истина;
	НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
	
	ОтчетТ2.ИнициализироватьОтчет();
	ОтчетТ2.КомпоновщикНастроек.ЗагрузитьНастройки(
	ОтчетТ2.СхемаКомпоновкиДанных.ВариантыНастроек.Т2.Настройки);
	Отбор = ОтчетТ2.КомпоновщикНастроек.Настройки.Отбор;
	Отбор.Элементы.Очистить();
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, "РабочееМесто.Сотрудник", ВидСравненияКомпоновкиДанных.ВСписке, МассивОбъектов);
	ОтчетТ2.СкомпоноватьРезультат(ДокументРезультат);
	
	Возврат ДокументРезультат;
	
КонецФункции


